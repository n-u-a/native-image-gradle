
plugins {
	id 'org.springframework.boot' version '2.6.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.springframework.experimental.aot' version '0.11.3'
	id 'java'
}

apply plugin: 'application'

group = 'com.native'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/release' }
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
	gradlePluginPortal()
}

ext {
	set('springCloudVersion', "2021.0.1")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {

	// Spring Framework
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.cloud:spring-cloud-function-context'
	implementation group: 'org.springframework', name: 'spring-web', version: '5.3.16'
	implementation group: 'org.springframework.cloud', name: 'spring-cloud-function-adapter-aws', version: '3.2.1'

	// AWS
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '3.11.0'
	implementation group: 'com.amazonaws', name: 'aws-lambda-java-serialization', version: '1.0.0'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// mybatis
	implementation 'org.mybatis:mybatis-spring:2.0.7'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
	implementation 'org.mybatis.spring.native:mybatis-spring-native-extensions:0.1.0-SNAPSHOT'

	// JDBC
	implementation group: 'org.springframework', name: 'spring-jdbc', version: '5.3.18'
	implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'

	// MySQL
	implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.21'

	// other
	implementation 'org.apache.commons:commons-lang3:3.12.0'

	// test
	testImplementation 'junit:junit:4.13.1'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.6'
	testImplementation 'com.ninja-squad:DbSetup:2.1.+'

}

tasks.named('test') {
	useJUnitPlatform()
}

graalvmNative {
	binaries {
		main {
			// Main options
			imageName = 'application' // The name of the native image, defaults to the project name

			configurationFileDirectories.from(file('src/main/resources/META-INF')) // Adds a native image configuration file directory, containing files like reflection configuration
		}
	}
}

bootBuildImage {
	builder = "paketobuildpacks/builder:tiny"
	environment = [
			"BP_NATIVE_IMAGE" : "true"
	]
	buildpacks = ["gcr.io/paketo-buildpacks/java-native-image:7.1.0"]
}

aotTest {
	jvmArgs "-agentlib:native-image-agent=config-output-dir=./src/main/resources/META-INF,caller-filter-file=./src/main/resources/META-INF/filter.json"
}

task buildZip(type: Zip) {
	mustRunAfter check // testの前に長時間buildをしなくていいように
	dependsOn nativeCompile
	from "./build/native/nativeCompile"
	from "./bootstrap"
	archiveFileName = 'lambdaPackage.zip'
}
